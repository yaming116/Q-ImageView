apply plugin: 'com.android.library'
apply plugin: 'com.github.dcendents.android-maven'
apply plugin: 'com.jfrog.bintray'

android {
    compileSdkVersion 23
    buildToolsVersion "23.0.3"

    defaultConfig {
        minSdkVersion 14
        targetSdkVersion 23
        versionCode 1
        versionName "1.0"
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
    testCompile 'junit:junit:4.12'
}



def siteUrl = 'https://github.com/yaming116/Q-ImageView'      // 项目的主页
def gitUrl = 'https://github.com/yaming116/Q-ImageView.git'
// Git仓库的url
group  = 'me.shenfan.image'
version  = '1.0.0'

install {
    repositories.mavenInstaller {
        // This generates POM.xml with proper parameters
        pom {
            project {
                packaging 'aar'
                // Add your description here
                name 'warpper api for qcloud'
                url siteUrl
                artifactId = 'qiniu'
                // Set your license
                licenses {
                    license {
                        name 'The Apache Software License, Version 2.0'
                        url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }
                developers {
                    developer {
                        id 'yaming116'        //填写的一些基本信息
                        name '花开堪折枝'
                        email 'yaming1106@gmail.com'
                    }
                }
                scm {
                    connection gitUrl
                    developerConnection gitUrl
                    url siteUrl
                }
            }
        }
    }
}
task sourcesJar(type: Jar) {
    from android.sourceSets.main.java.srcDirs
    classifier = 'sources'
}

artifacts {
    archives sourcesJar
}

bintray {
    user = System.getenv('bintray_user')
    key = System.getenv("bintray.apikey")

    println "user: " + user
    println "key: " + key

    configurations = ['archives']
    pkg {
        repo = "maven"
        name = "qiniu"    //发布到JCenter上的项目名字
        websiteUrl = siteUrl
        vcsUrl = gitUrl
        licenses = ["Apache-2.0"]
        publish = true

    }
}

android.libraryVariants.all  { variant ->
    def name = variant.buildType.name
    if (!name.equals("debug")) {
        def task = project.tasks.create  "jar${name.capitalize()}", Jar
        task.dependsOn variant.javaCompile
        task.from  variant.javaCompile.destinationDir
        artifacts.add('archives', task);
    }
}
